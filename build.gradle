plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.sonarqube' version '7.2.2.6593'
}

group = 'com.tril'
version = '0.0.1-SNAPSHOT'
description = 'Billing & Notification Platform'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacoco {
    toolVersion = "0.8.12"
}

def jacocoExcludes = [
        '**/*Application*',
        '**/global/**',
        '**/config/**',
        '**/dto/**',
        '**/exception/**',
        '**/generated/**'
]

test {
    useJUnitPlatform()

    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    dependsOn test

    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: jacocoExcludes)
    }))

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    //finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: jacocoExcludes)
    }))

    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.75
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.75
            }
        }
    }
}

tasks.named('check') {
    //dependsOn 'jacocoTestCoverageVerification'
}

sonar {
    properties {
        property "sonar.projectKey", "Team-TR1L_TR1L-BE"
        property "sonar.organization", "tr1l"
        property "sonar.host.url", "https://sonarcloud.io"


        property "sonar.coverage.jacoco.xmlReportPaths",
                "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"


        property "sonar.exclusions", "**/build/**,**/.gradle/**"
    }
}

tasks.named("sonar") {
    dependsOn tasks.named("jacocoTestReport")
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    //default : java library 취급
    apply plugin: "java-library"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        testCompileOnly 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly  'org.junit.platform:junit-platform-launcher'

        //TSID
        implementation 'io.hypersistence:hypersistence-utils-hibernate-60:3.5.1'
        implementation 'com.github.f4b6a3:tsid-creator:5.2.6'

        //spring Modulith
        implementation 'org.springframework.modulith:spring-modulith-starter-core:2.0.1'
        testImplementation 'org.springframework.modulith:spring-modulith-starter-test:2.0.1'
    }
}


//app module boot 적용
configure(subprojects.findAll { it.path.startsWith(":apps") }) {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named("jar") { enabled = false }
    tasks.named("bootJar") { enabled = true }

    tasks.named("bootJar"){
        archiveFileName = "app.jar"
    }
}